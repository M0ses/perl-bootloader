#! /usr/bin/perl -w

use POSIX;
use Getopt::Long;
use Pod::Usage;
use Bootloader::Tools;
use Locale::gettext;
use strict;

my ($opt_add, $opt_default, $opt_help, $opt_image, $opt_initrd,
    $opt_man, $opt_name, $opt_previous, $opt_remove) =
    (0,0,0,'','',0,'',0,0);

=head1 NAME

update-bootloader - update/change bootloader configuration using
    Bootloader::Tools perl module

 Using Getopt::Long and Pod::Usage

=head1 SYNOPSIS

update-bootloader [options]

Options:
   --help            brief help message
   --man             full documentation
   --image <file>
   --initrd <file>
   --default
   --previous
   --add
   --remove

=head1 OPTIONS

=over 8

=item B<--help>

Print a brief help message and exits.

=item B<--man>

Prints the manual page and exits.
   
=item B<--image <file>>

=item B<--initrd <file>>

=item B<--default>

=item B<--previous>

=item B<--add>

=item B<--remove>

=back

=head1 DESCRIPTION

B<This program> will let you modify your bootloader configuration using
Bootloader::Tools perl module.

=cut

$opt_default = 0;
$opt_previous = 0;

setlocale(LC_MESSAGES, Bootloader::Tools::GetSystemLanguage());

my $d =  Locale::gettext->domain("bootloader");
$d->dir("/usr/share/YaST2/locale");

GetOptions (
    'help|h'   	    => \$opt_help,
    'man|m'    	    => \$opt_man,
    'add|a'    	    => \$opt_add,
    'remove|r' 	    => \$opt_remove,
    'image=s'  	    => \$opt_image,
    'initrd=s' 	    => \$opt_initrd,
    'name=s'   	    => \$opt_name,
    'default|def'   => \$opt_default,
    'previous|prev' => \$opt_previous)
    or pod2usage(2);
pod2usage(1) if $opt_help;
pod2usage(-exitstatus => 0, -verbose => 2) if $opt_man;

pod2usage("Need at least one of the options 'add', 'image', 'previous' or 'remove'")
    unless ($opt_image or $opt_previous or $opt_add or $opt_remove);

if ($opt_image)
{
    $opt_image = getcwd . '/' . $opt_image
    if ($opt_image !~ m#^/#);
}

if ($opt_initrd)
{
    $opt_initrd = getcwd . '/' . $opt_initrd
    if ($opt_initrd !~ m#^/#);
}

InitLibrary();

my $loader = Bootloader::Tools::GetBootloader();

if ($opt_previous)
{
    unless ($opt_image)	{
    	$opt_image = Bootloader::Tools::GetDefaultImage() . ".previous";
    	$opt_initrd = Bootloader::Tools::GetDefaultInitrd() . ".previous";
    }	
}

if ($opt_add)
{
    if ($opt_previous)
    {
        $opt_name = "Previous Kernel";

        # only localize on grub and lilo
    	if ($loader eq "grub" || $loader eq "lilo")
    	{
    	    $opt_name = $d->get($opt_name);
            chomp ($opt_name);
	    
            # check wether translation only contains [a-zA-Z0-9 _]
            # otherwise don't translate
            if ($opt_name !~ /^[a-zA-Z0-9 _]+$/g )
            {
                $opt_name = "Previous Kernel";
            }

            if($loader ne "grub")
            {
                #only grub allows blanks in section titles
                $opt_name =~ s/ /_/g;
                #restrict lenght to 16 chars
                $opt_name = substr ($opt_name, 0, 15);
            }
        }
        # add product name and version
        $opt_name .= `yast2 /usr/share/YaST2/modules/print-product.ycp 2>&1`;
        chomp $opt_name;
    }
    else
    {
    	unless ($opt_name)
    	{
            $opt_name = $opt_image;
    	}
    }

    unless (CountImageSections($opt_image))
    {
        AddNewImageSection($opt_name, $opt_image, $opt_initrd, $opt_default);
    }

}
elsif ($opt_remove)
{
    if (CountImageSections($opt_image))
    {
        RemoveImageSections($opt_image);
    }
}

#
# Local variables:
#     mode: perl
#     mode: font-lock
#     mode: auto-fill
#     fill-column: 78
# End:
#
